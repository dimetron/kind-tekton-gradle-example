ext {
    clusterTestPassed = false
    userHome = isWindows ? System.getenv('USERPROFILE') : System.getenv('HOME')
}

//test tools UP and k8s is alive
task verify_tekton_cluster  {
    doLast {
        try {
            new File(".gradle/cluster").delete()
            String cli = sh(script: "docker ps -q -a --no-trunc --filter name=$shell_cli", localMode: true, returnStdout: true).trim()
            if (cli.length() > 0) {
                clusterTestPassed = sh(script: "kind get nodes --name ${project.name} 2>/dev/null || :", returnStdout: true).length() > 0
                new File(".gradle/cluster") << "$cli $clusterTestPassed"
            }
        } catch(Exception e) {
            println "Tekton cluster not found ${e.message}"
        }
    }
}

//create local k8s cluster  in Docker and execute hello gradle pipeline in k8s
task create_tekton_cluster(dependsOn: verify_tekton_cluster) {
    //skip if cluster is up
    onlyIf = { !project.properties['clusterTestPassed'] }

    // prepare tools container
    doFirst {
        def logs = new File("$workspace/logs").absolutePath
        def tekton = new File("$workspace/tekton").absolutePath
        def tools = new File("$workspace/tools").absolutePath

        def cmd = "docker run -d --name $shell_cli --restart=always --privileged --net=host"
        cmd = "$cmd -v $logs:/root/logs"
        cmd = "$cmd -v $tools:/root/tools"
        cmd = "$cmd -v $tekton:/root/tekton"
        cmd = "$cmd -v /var/run/docker.sock:/var/run/docker.sock dev-tools:latest"

        sh(script: "docker rm  -f $shell_cli", localMode: true,  returnStatus: true, returnStderr: true)
        sh(script: "$cmd", localMode: true, returnStderr: true).trim()
        sh(script: "kind delete cluster --name ${project.name} 2>&1", returnStatus: true)

        sh """
        kind create cluster --name ${project.name} --config=/root/tools/kind-config.yaml --wait 2m 2>&1
        mkdir -p ~/.kube
        kind get kubeconfig --name ${project.name} > ~/.kube/config
        """
    }
}

task set_tekton_namespace(dependsOn: create_tekton_cluster) {
    doLast {
        //copy kubectl from container to the host
        sh "kubectl get ns tekton-pipelines || kubectl create ns tekton-pipelines"
        sh "kubectl config set-context --current --namespace=tekton-pipelines"
        def configFile = new File("$userHome/.kube/config")
        configFile.mkdirs()
        configFile.delete()
        String config = sh(script: "cat ~/.kube/config", returnStdout: true)
        configFile << config
    }
}

task install_tekton(dependsOn: set_tekton_namespace) {
    group = 'tekton-cd'
    inputs.dir('tekton/app')

    doFirst {
        println"${styler['blue']('Deploying Tekton')} ${styler['cyan']('...')}\n"
        def resources = ['pipelines', 'dashboard', 'ingress', 'triggers']
        resources.each { kind ->
            println"${styler['blue']("Deploying")} ${styler['cyan']("$kind")}"
            sh "kapp deploy -y -a tekton-app-${kind} -n tekton-pipelines -f tekton/app/${kind}/release.yaml"
        }
    }
}

def startPipeline(String name) {
    println"${styler['red']("Running pipeline ")} ${styler['cyan']("$name ...")}"
    sh "tkn pipeline start $name"
}

//task starts tekton pipeline in kind k8s cluster
tasks.register('create_pipelines') {
    group = 'tekton-cd'
    inputs.dir('tekton/impl')

    doFirst {
        def resources = ['resource', 'condition', 'task', 'pipeline', 'trigger']
        resources.each { kind ->
            println "${styler['blue']("Deploying")} ${styler['cyan']("$kind")}"
            sh "kapp deploy -y -a tekton-impl-${kind} -f tekton/impl/$kind -n tekton-pipelines"
        }
    }
}

//run pipeline
tasks.register('pipeline_run') {
    group = 'tekton-cd'
    dependsOn = ['create_pipelines']
    doLast {
        //for pipeline run we delete and create
        println"${styler['green']("Cleanup")} ${styler['cyan']("...")}"

        sh "tkn pipelinerun delete  --all -f"
        startPipeline "print-date-pipeline"
        startPipeline "gradle-pipeline"
    }
}